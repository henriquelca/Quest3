extends CharacterBody2D
class_name Player

var gravity = ProjectSettings.get_setting("physics/2d/default_gravity")

enum PlayerMode {
	SMALL,
	BIG,
	SHOOTING
}

signal points_scored(points: int)

const POINTS_LABEL_SCENE = preload("res://Cenas/points_label.tscn")

@onready var animated_sprite_2d = $AnimatedSprite2D as PlayerAnimatedSprite
@onready var area_collision_shape_2d = $Area2D/AreaCollisionShape2D
@onready var body_collision_shape_2d = $BodyCollisionShape2D
@onready var area_2d = $Area2D

@export_group("Locomotion")
@export var run_speed_damping = 0.5
@export var speed = 100.0
@export var jump_velocity = -350
@export_group("")

@export_group("Stomping Enemies")
@export var min_stomp_degree = 35
@export var max_stomp_degree = 145
@export var stomp_y_velocity = -150
@export_group("")

var player_mode = PlayerMode.SMALL
var is_dead = false
var is_transforming = false

func _physics_process(delta):
	if is_transforming:
		return

	if not is_on_floor():
		velocity.y += gravity * delta
		
	if position.y > 100 and not is_dead:
		die()
		return
	
	if Input.is_action_just_pressed("jump") and is_on_floor():
		velocity.y = jump_velocity
		
	if Input.is_action_just_released("jump") and velocity.y < 0:
		velocity.y *= 0.5

	if is_on_ceiling():
		for i in get_slide_collision_count():
			var colisao = get_slide_collision(i)
			var collider = colisao.get_collider()
			if collider and collider.has_method("bater_por_baixo"):
				collider.bater_por_baixo()
		
	var direction = Input.get_axis("left", "right")
	
	if direction:
		velocity.x = lerp(velocity.x, speed * direction, run_speed_damping * delta)
	else:
		velocity.x = move_toward(velocity.x, 0, speed * delta)
		
	animated_sprite_2d.trigger_animation(velocity, direction, player_mode)
	move_and_slide()

func _on_area_2d_area_entered(area):
	if area is Enemy:
		handle_enemy_collision(area)

func handle_enemy_collision(enemy: Enemy):
	if enemy == null || is_dead:
		return
	
	var angle_of_collision = rad_to_deg(position.angle_to_point(enemy.position))
	var is_stomping = angle_of_collision > min_stomp_degree && max_stomp_degree > angle_of_collision
	
	if is_instance_of(enemy, Koopa) and (enemy as Koopa).in_a_shell:
		if (enemy as Koopa).horizontal_speed == 0:
			(enemy as Koopa).on_stomp(global_position)
		else:
			if is_stomping:
				(enemy as Koopa).horizontal_speed = 0
				on_enemy_stomped()
			else:
				die()
	else:
		if is_stomping:
			enemy.die()
			on_enemy_stomped()
			spawn_points_label(enemy)
		else:
			die()

func on_enemy_stomped():
	velocity.y = stomp_y_velocity
	
func spawn_points_label(enemy):
	var points_label = POINTS_LABEL_SCENE.instantiate()
	points_label.position = enemy.position + Vector2(-20, -20)
	get_tree().root.add_child(points_label)
	points_scored.emit(100)

func die():
	if player_mode == PlayerMode.SMALL:
		is_dead = true
		animated_sprite_2d.play("small_death")
		set_physics_process(false)
		
		var death_tween = get_tree().create_tween()
		death_tween.tween_property(self, "position", position + Vector2(0, -48), .5)
		death_tween.chain().tween_property(self, "position", position + Vector2(0, 256), 1)
		death_tween.tween_callback(func (): GameManager.lose_life())
	else:
		player_mode = PlayerMode.SMALL
		
		var nova_forma_corpo = RectangleShape2D.new()
		nova_forma_corpo.size = Vector2(16, 16)
		body_collision_shape_2d.shape = nova_forma_corpo
		body_collision_shape_2d.position.y = 0
		
		var nova_forma_area = RectangleShape2D.new()
		nova_forma_area.size = Vector2(16, 16)
		area_collision_shape_2d.shape = nova_forma_area
		area_collision_shape_2d.position.y = 0
		
		animated_sprite_2d.position.y = 0
		
		area_2d.set_deferred("monitoring", false)
		await get_tree().create_timer(1.0).timeout
		area_2d.set_deferred("monitoring", true)

func crescer():
	if player_mode == PlayerMode.SMALL:
		is_transforming = true
		animated_sprite_2d.play("small_to_big")
		
		var nova_forma_corpo = RectangleShape2D.new()
		nova_forma_corpo.size = Vector2(16, 32)
		body_collision_shape_2d.shape = nova_forma_corpo
		body_collision_shape_2d.position.y = -8
		
		var nova_forma_area = RectangleShape2D.new()
		nova_forma_area.size = Vector2(16, 32)
		area_collision_shape_2d.shape = nova_forma_area
		area_collision_shape_2d.position.y = -8
		
		animated_sprite_2d.position.y = -8
		
		await animated_sprite_2d.animation_finished
		player_mode = PlayerMode.BIG
		is_transforming = false
